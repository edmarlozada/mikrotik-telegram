# === Telegram_Function === #

# eTGSendAPI #
# ------------------------------
# Send Telegram Message API v6.2
# by: Chloe Renae & Edmar Lozada
# ------------------------------
:local tMsg $1; :local gTGResult;
:local iURL ("https://api.telegram.org"."/bot$tBot/sendmessage\?chat_id=$tGrp&text=$tMsg");
:local iCtr 3; :while ($iCtr > 0) do={
  :do {
    :set gTGResult ([/tool fetch url=$iURL as-value output=user]);
  } on-error={
    set iCtr 0;
  };
  :local iStatus ($gTGResult->"status");
  :if ($iStatus="finished") do={
    :set iCtr -9; };
  :if (($iCtr > 0) and ($iStatus!="finished")) do={
    :set iCtr ($iCtr-1) };
  :if (($iCtr = 0) and ($iStatus!="finished")) do={
    :log warning "(eTGSendAPI) error: sending failed => [$iCtr] [$iStatus]" };
};
:return $iCtr;
# ------------------------------


# eTGSave #
# ------------------------------
# Save Telegram Message v6.2
# by: Chloe Renae & Edmar Lozada
# ------------------------------
:local eRand do={:local n [/system resource irq get 0 count];:return ([pick $n ([:len $n]-2) [:len $n]])};
:local iDate do={:local d [/system clock get date];:local cD [:pic $d 4 6];:local cY [:pick $d 7 11];
  :local cM [pick (100+([find "..anebarprayunulugepctovec" [:pick [:pick $d 0 3] 1 3]]/2)) 1 3];:return "$cY$cM$cD"};
:local iTime do={:local t [/system clock get time];:local r "";
  :for i from=0 to=([:len $t]-1) do={:local x [:pick $t $i];:if ($x!=":") do={:set r "$r$x"};};:return $r};

:local tMsg $1;
:if (([:len $tBot]=0) || ([:len $tGrp]=0) || ([:len $tMsg]=0)) do={
  :log warning "(eTGSave) error: input empty"; :return 0;
};
:local tName ("TG_".[$iDate]."_".[$iTime]."_".[$eRand]);
:local tSave (":local tData {\r\n".\
              "  \"tBot\"=\"$tBot\";\r\n".\
              "  \"tGrp\"=\"$tGrp\";\r\n".\
              "  \"tMsg\"=\"$tMsg\" \r\n".\
              "}; :return \$tData\r\n");
/system script add name=$tName source=$tSave owner="telegram savedata" comment="telegram_savedata: $tGrp";
:local n 5;:while (($n>0) and ([/system script find name=$tName]="")) do={:set n ($n-1);:delay 1s};
:if ($n=0) do={
  :log warning "(eTGSave) error: /system-script NOT saved => name:[$tName]";
} else={
  /system scheduler set [find name=eTGHandler] disabled=no;
};
# ------------------------------


# eTGRemoveAll #
# ------------------------------
# Remove all Telegram Messages v6.2
# by: Chloe Renae & Edmar Lozada
# ------------------------------
:foreach nRec in=[/system script find owner="telegram savedata"] do={ /system script remove $nRec };
# ------------------------------


# eTGSend #
# ------------------------------
# Send Telegram Message v6.2
# by: Chloe Renae & Edmar Lozada
# ------------------------------
:local tMsg $1; :local iCtr 3;
:if (([:len $tBot]=0) || ([:len $tGrp]=0) || ([:len $tMsg]=0)) do={
  :log warning "(eTGSend) error: input empty"; :return 0;
};
:if ([/ping 1.1.1.1 count=1]=0) do={ :if ([/ping 8.8.8.8 count=2]=0) do={
  :set iCtr 0; :log warning "(eTGSend) error: no internet [ping=0]";
}};
:if ($iCtr > 0) do={
  :local eTGSendAPI [:parse [/system script get sc-eTGSendAPI source]];
  :set iCtr [$eTGSendAPI tBot=$tBot tGrp=$tGrp $tMsg];
};
:if ($iCtr=0) do={
  :local eTGSave [:parse [/system script get sc-eTGSave source]];
  $eTGSave tBot=$tBot tGrp=$tGrp $tMsg;
};
# ------------------------------

